# MATCH

The `MATCH` statement provides the searching ability based on pattern matching.

A `MATCH` statement defines a search pattern and uses it to match data stored in the Nebula Graph database and to retrieve the data in the form defined in the `RETURN` clause. A `WHERE` clause is often used together with the pattern as a filter to the search result.

## Syntax

The syntax of `MATCH` is relatively more flexible compared with that of other query statements such as `GO` or `LOOKUP`. But generally, it can be summarized as follows.

```nGQL
MATCH <pattern> [<WHERE clause>] RETURN <output>
```

## Patterns and WHERE clauses

The `MATCH` patterns, with the help of curly brackets ("{}"), can do the same thing with `WHERE` clauses when making equality comparisons. You may find an example as follows.

> **NOTE**:
>
> * In nGQL, `==` is the equality operator and `=` is the assignment operator (as in C++ or Java).
> * In openCypher 9, `=` is the equality operator.

```nGQL
nebula> MATCH (v:player) WHERE v.name == "Tim" RETURN v.age AS Age;
+-----+
| Age |
+-----+
| 42  |
+-----+
Got 1 rows (time spent 2849/3256 us)

nebula> MATCH (v:player {name: "Tim"}) RETURN v.age AS Age;
+-----+
| Age |
+-----+
| 42  |
+-----+
Got 1 rows (time spent 1924/2673 us)
```

But besides equality comparisons, `WHERE` clauses can do a lot more than patterns in curly brackets. For more information, see [WHERE](TODO) [TODO].

## Match modes

Currently, `MATCH` supports the following modes.

|Match mode|Description|
|-|-|
|Match on property|Uses specific properties to match the start vertices, then expand the pattern to match related edges or vertices if needed, and fetch data according to the `RETURN` statement at last.|
|Match on ID|Uses specific vertex IDs to match the start vertices, then expand the pattern to match related edges or vertices if needed, and fetch data according to the `RETURN` statement at last.|

## Must-read for matching on property

The `MATCH` statement is based on indexes. While parsing a `MATCH` statement, the Graph Service scans for properties with indexes in the curly brackets and the `WHERE` clause. Make sure there is at least one property with an Index, otherwise the parsing fails.

No such attention is needed when matching on ID because the index for vertex IDs is created by default.

### Examples for matching on property

* Example 1: Search for a specific age and return the names of players of that age. The `v` is a user-defined variable that represents a vertex.

    ```nGQL
    nebula> MATCH (v:player {age: 36}) RETURN v.name AS Name;
    +-------------+
    | Name        |
    +-------------+
    | Tony Parker |
    +-------------+
    Got 1 rows (time spent 2849/3256 us)
    ```

* Example 2: Search for the players whose ages are above 30 and names start with "T", and return their names and ages. Use `STARTS WITH` or `ENDS WITH` to filter strings.

    ```nGQL
    nebula> MATCH (v:player) WHERE v.age > 30 AND v.name STARTS WITH "T" \
         ->RETURN v.name AS Name, v.age AS Age;
    +-------------+-----+
    | Name        | Age |
    +-------------+-----+
    | Tony Parker | 36  |
    +-------------+-----+
    | Tim         | 42  |
    +-------------+-----+
    Got 2 rows (time spent 2407/3244 us)
    ```

* Example 3: Search for the player whose name is "Tim", and return the player's relationships (edges) with others and the names of these people. The `r` is a user-defined variable that represents an edge. The `type(e)` function returns to the edge type.

    ```nGQL
    nebula> MATCH (v1:player{name: "Tim"}) -[e]-> (v2) \
         -> RETURN type(e) AS Relationship, v2.name AS Name;
    +--------------+-------------------+
    | Relationship | Name              |
    +--------------+-------------------+
    | follow       | Tony Parker       |
    +--------------+-------------------+
    | follow       | LaMarcus Aldridge |
    +--------------+-------------------+
    | serve        | Warriors          |
    +--------------+-------------------+
    Got 3 rows (time spent 4844/6660 us)
    ```

* Example 3: Search for the team that "Tim" serves, and return the team name, the start year, and the end year. `e:serve` represents edges with the `serve` type.

    ```nGQL
    nebula> MATCH (v1:player{name: "Tim"}) -[e:serve]-> (v2) \
         -> RETURN v2.name AS Team_Name, e.start_year AS Start_Year, e.end_year AS End_Year;
    +-----------+------------+----------+
    | Team_Name | Start_Year | End_Year |
    +-----------+------------+----------+
    | Warriors  | 1997       | 2016     |
    +-----------+------------+----------+
    Got 1 rows (time spent 2768/3412 us)
    ```

* Example 4: Search for the player whose name is "Tim", find the people that "Tim" follows with age above 35 and the teams they serve, and return the names of the people and teams.

    ```nGQL
    nebula> MATCH (v1:player) -[:follow]-> (v2) -[:serve]-> (v3) \
         -> WHERE v1.name == "Tim" AND v2.age > 35 \
         -> RETURN v1.name AS Player, v2.name AS Friend, v3.name AS Friends_Team;
    +--------+-------------+--------------+
    | Player | Friend      | Friends_Team |
    +--------+-------------+--------------+
    | Tim    | Tony Parker | Nuggets      |
    +--------+-------------+--------------+
    Got 1 rows (time spent 3623/4220 us)
    ```

### Examples for matching on ID

* Example 1: Search for the vertex with VID "player100" and return all its information.

> **NOTE**:
>
> * In nGQL, `id()` returns a vertex ID that is a string.
> * In openCypher 9, `id()` returns an integer.


    ```nGQL
    nebula> MATCH (v) WHERE id(v) == 'player101' RETURN v;
    +---------------------------------------------------+
    | v                                                 |
    +---------------------------------------------------+
    | (player101) player.name:Tony Parker,player.age:36 |
    +---------------------------------------------------+
    Got 1 rows (time spent 1710/2406 us)
    ```

* Example 2: Search for the vertices with VIDs in a specific list and return all their information. A non-existing VID returns nothing.

    ```nGQL
    nebula> MATCH (v) WHERE id(v) IN ['player100', 'player101', 'no_one'] RETURN v;
    +---------------------------------------------------+
    | v                                                 |
    +---------------------------------------------------+
    | (player101) player.name:Tony Parker,player.age:36 |
    +---------------------------------------------------+
    | (player100) player.name:Tim,player.age:42         |
    +---------------------------------------------------+
    Got 2 rows (time spent 1307/2079 us)
    ```
